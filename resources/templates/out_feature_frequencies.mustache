{{!
@param IRI source-graph
@param IRI target-graph
@param IRI property
@param int limit
@param int offset
}}

SELECT ?resource (<{{property}}> AS ?property) (COUNT(DISTINCT ?s) AS ?frequency)
WHERE {
  {
    SELECT ?resource
    WHERE {
      {
        SELECT DISTINCT ?resource
        WHERE {
          GRAPH <{{target-graph}}> {
            [] <{{property}}> ?resource . 
          }
        }
        ORDER BY ?resource
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
  GRAPH <{{source-graph}}> {
    ?s <{{property}}> ?resource .
    FILTER (isIRI(?s))
  }
}
GROUP BY ?resource

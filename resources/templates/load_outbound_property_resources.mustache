{{!
@param IRI        source-graph
@param IRI        target-graph
@param IRI        property
@param Array<IRI> classes
@param Boolean    virtuoso
}}

{{#virtuoso}}
DEFINE sql:log-enable 2
{{/virtuoso}}

INSERT {
  GRAPH <{{target-graph}}> {
    <{{target-graph}}> <{{property}}> ?resource .
  }
}
WHERE {
  {
    SELECT ?resource
    WHERE {
      {
        SELECT DISTINCT ?resource
        WHERE {
          GRAPH <{{source-graph}}> {
            VALUES ?class {
              {{#classes}}
              <{{.}}>
              {{/classes}}
            }
            [] a ?class ;
              <{{property}}> ?resource .
            FILTER (isIRI(?resource))
          }
        }
        ORDER BY ?resource
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
}
